---
layout: post
title: Terje Mathisen's 1997 number-to-string converter
date:  2018-05-10
---

Benchmarking a program and finding the bottleneck to be (how?!) `itoa`-like functions?
Looking to convert lots of numbers _fast_? Terje Mathisen invented something in 1997 to
help you with that!

(Disclaimer: This is all my opinion. It's a _blog_, for heaven's sake!)
  
TL;DR See [this](http://computer-programming-forum.com/46-asm/7aa4b50bce8dd985.htm).

The easiest way to solve the problem of number-to-string conversion is a div/mod
combination, where you fill in the text backwards (or reverse it, your choice). But if you
look at any good assembly optimization book (such as
[Agner Fog's guide](http://www.agner.org/optimize/optimizing_assembly.pdf)), you'd find
that divisions (which automatically also return the mod) are _expensive_. They take 20-40
CPU cycles when independent, and 20-80 when dependent on previous values! On the other
hand, multiplications take just 1-2 independent cycles and 3-10 dependent! Terje realised
that the only fast way to perform division was using bit shifting, which takes a maximum of
1 clock cycle, either dependent or independent. He experimented on combining shifting and
multiplication and found that an easy way to divide integers fast is to multiply them by
`2^x/<divisor>`, and shifting the result `x` bits to the right. With slight adjustment to
the input, it works much faster! So, he designed a number-converting function for base 10
that accepts numbers up to 10^5, where it multiplies by 2^32/10^4 to form a 4.28 floating
point number, with the highest digit overflowing (over 32-bit registers). Then, you
repeatedly multiply the rest by 10 and put `(num >> 28) + '0'` into the buffer, after which
you bitwise AND the rest by `~((2^4 - 1) << 28)` to remove the digit we just input into the
buffer.

To speed it up, he split it into two sets of numbers, the high 10^5 and the low 10^5.
First, he multiplies by 2^48/10^5, then he shifts to the right by 48 to get the high part,
then we multiply it back by 10^5 and subtract it from the number to get the remainder.

Here's the 32-bit code he posted, in C ('(c) Terje Mathisen, 1997'):  
(PS, his code on the forum is badly formatted, so I reformatted it)  
(PPS, all code comments are mine)
``` c
char *dtoa_c(char *buf, unsigned long n)
{
  unsigned _int64 l64, h64;
  unsigned long l, h, i;
  l64 = n;
  l64 *= 2814749767; // 2^48 / 10^5
  l64 += n >> 1; // adjust
  h = (unsigned long) (l64 >> 48);
  l = n - h * 100000;
  l64 = l;
  h64 = h;
  h64 *= 429497; // 2^32 / 10^4
  l64 *= 429497; // 2^32 / 10^4
  buf[0] = (char)(h64 >> 32) + '0';
  buf[5] = (char)(l64 >> 32) + '0';
  h = (unsigned long) h64;
  l = (unsigned long) l64;
  h = (h + 15) >> 4; // adjust
  l = (l + 15) >> 4; // adjust
  for (i = 1; i < 5; i++) {
    h *= 10;
    l *= 10;
    buf[i]   = (char)(h >> 28) + '0';
    buf[i+5] = (char)(l >> 28) + '0';
    h &= 0x0fffffff; // remove previous number
    l &= 0x0fffffff; // remove previous number
  }
  buf[10] = '\0';
  return buf;
}
```

He also provided an optimized version, which multiplies by 5. The advantage of multiplying
by 5 is that the `lea` instruction, which 'loads effective address', can be used to
multiply a register by 5 (for example, for `eax`: `leal (%eax,%eax,4), %eax` or
`lea eax,[eax+eax*4]`). To adjust for the x5 multiplication, the loop is unrolled and the
bitshifts in the loop decrease with every iteration (as one less bitshift is equivalent to
multiplying by 2) and the bitwise AND operation's operand is reduced by one bit (so if we
represented `0x0fffffff` as `0b0000_1111_1111_1111_1111_1111_1111_1111`, then then the next
operand is `0b0000_0111_1111_1111_1111_1111_1111_1111`, which is `0x07ffffff`). The
optimized version also adjusts before the loop with `(h + 7) >> 3`, which I don't
understand too well. Note that the optimized version is written in assembly.

Here it is ('(c) Terje Mathisen, 1997'):  
(PS: reformatted; comments to the right of a statement are Terje's)  
(PPS: I personally do not like Intel syntax, but I'm too lazy to convert to AT&T)  
``` c
char *dtoa(char *buf, unsigned long n)
{ __asm {
  // l64 = n
  mov ebx, [n]
  // l64 *= 2814749767
  mov eax, 2814749767
  mul ebx
  // l64 += n >> 1
  shr ebx, 1
  xor ecx, ecx
  mov edi, [buf]
  add eax, ebx
  adc ecx, edx
  // h = (unsigned long) (l64 >> 48); l = n - h * 100000
  mov eax, 100000
  shr ecx, 16           // ecx = high part
  mov ebx, [n]          // retrieve org. number
  imul eax, ecx         // High part * 100k
  sub ebx, eax          // Remainder = Low part
  // h64 *= 429497
  mov eax, 42497
  mul ecx
  // buf[0] = (char)(h64 >> 32) + '0'
  mov ecx, eax
  add dl,  '0'
  // interlaced: l64 *= 429497
  mov eax, 429497
  // previous buf[0] = ...
  mov [edi], dl
  // back to l64 *= ...
  mul ebx
  mov ebx, eax
  // h = (unsigned long) h64; h = (h + 7) >> 3
  add ecx, 7
  shr ecx, 3
  // buf[5] = (char)(l64 >> 32) + '0'
  add dl,  '0'
  mov [edi+5], dl
  // l = (unsigned long) l64; l = (l + 7) >> 3
  add ebx, 7
  shr ebx, 3
  // for loop iteration, i = 1
  // h *= 5
  lea ecx, [ecx+ecx*4]
  // buf[i=1] = (char)(h >> 28) + '0'
  mov edx, ecx
  // interlaced: h &= 0x0fffffff
  and ecx, 0fffffffh
  // back to buf[i=1] = ...
  shr edx, 28
  // interlaced: l *= 5
  lea ebx, [ebx+ebx*4]
  // back to buf[i=1] = ...
  add dl,  '0'
  // interlaced: buf[i=1+5] = (char)(l >> 28) + '0'
  mov eax, ebx
  shr eax, 28
  // back to buf[i=1] = ...
  mov [edi+1], dl
  // interlaced: l &= 0x0fffffff
  and ebx, 0fffffffh
  // back to buf[i=1+5] = ...
  add al, '0'
  mov [edi+6], al
  // for loop iteration, i = 2
  // h *= 5
  lea ecx, [ecx+ecx*4]
  // l *= 5
  lea ebx, [ebx+ebx*4]
  // buf[i=2] = (char)(h >> 27) + '0'
  mov edx, ecx
  // interlaced: buf[i=2+5] = (char)(l >> 27) + '0'
  mov eax, ebx
  // interlaced: h &= 0x07ffffff
  and ecx, 07ffffffh
  // back to buf[i=2] = ...
  shr edx, 27
  // interlaced: h &= 0x07ffffff
  and ebx, 07ffffffh
  // back to buf[i=2+5] = ...
  shr eax, 27
  // back to buf[i=2] = ...
  add dl,  '0'
  // back to buf[i=2+5] = ...
  add al,  '0'
  // back to buf[i=2] = ...
  mov [edi+2], dl
  // back to buf[i=2+5] = ...
  mov [edi+7], al
  // for loop iteration, i = 3
  // h *= 5
  lea ecx, [ecx+ecx*4]
  // l *= 5
  lea ebx, [ebx+ebx*4]
  // buf[i=3] = (char)(h >> 26) + '0'
  mov edx, ecx
  // interlace: buf[i=3+5] = (char)(l >> 26) + '0'
  mov eax, ebx
  // interlace: h &= 0x03ffffff
  and ecx, 03ffffffh
  // back to buf[i=3+5] = ...
  shr eax, 26
  // WTF is this? dl contains the low bit of h
  add dl,  '0'
  // back to buf[i=3+5] = ...
  add al,  '0'
  // Back to the WTF line
  mov [edi+3], dl
  // back to buf[i=3+5] = ...
  mov [edi+8], al
  // h *= 5
  lea ecx, [ecx+ecx*4]
  // back to buf[i=3] = ...
  shr ecx, 25
  // l *= 5
  lea ebx, [ebx+ebx*4]
  // l >>= 25
  shr ebx, 25
  // Beyond this, I'm lost. Use the normal shifting stuff above and don't save.
  add cl,  '0'
  add bl,  '0'
  mov [edi+10], ah
  mov [edi+4],  cl
  mov [edi+9],  bl
} return buf; }
```

This works in 32-bit, but these days many computers have moved on to 64-bit where this
won't work for all possibilities (2^64 - 2^32 of them). I spent a few hours looking at the
code and his descriptions to understand how to change the black magic numbers until I
understood how it works. I then rewrote the program in C in 64-bit, with bugs. I wrote it
again in 64-bit, with the same optimizations as the assembly above. It works, but when I
was testing it wouldn't go beyond 9223372036854775807 (correctly formatted), until I
realised that I was using `atoll`, which returned a 64-bit signed integer number, so it is
limited to 2^63-1 (which is the number above). Please use `strtoull` instead!

I'd like to mention his postscript-thing at the end of the post:
> Using self-discipline, see http://www.eiffel.com/discipline  
> "almost all programming can be viewed as an exercise in caching"
